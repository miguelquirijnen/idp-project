vocabulary V {
    type Node
    Edge(Node, Node)
    EdgeOnPath(Node, Node)
    Start:Node
    End:Node
}

theory PathTheory : V {
    // at most one outgoing edge
    ! x: #{y: EdgeOnPath(x, y)} =< 1.
    // at most one incoming edge
    ! y: #{x: EdgeOnPath(x, y)} =< 1.
    
    // every connection in the path is an edge
    ! x y: EdgeOnPath(x, y) => Edge(x, y).
    
    // no edge before start
    ! x: ~EdgeOnPath(x, Start).
    // no edge after end
    ! y: ~EdgeOnPath(End, y).

    // the path has an edge from start
    ? y: EdgeOnPath(Start, y).
    
    // if a node is an endpoint of an edge in the path, then it is also a start
    // point for a next edge except if the node is the end
    ! y: (y ~= End & ? x: EdgeOnPath(x, y)) => (? z: EdgeOnPath(y, z)).
}

term Length : V {
    #{x y : EdgeOnPath(x, y)}
}


structure S : V {
    Node = {1..7}
    Edge = {1,2;2,3;3,4;4,5;5,2;5,1;4,2;2,5;6,7}
    Start = 1
    End = 5
}

include <mx>

procedure main(){
    stdoptions.nbmodels = 0
    printmodels(minimize(PathTheory, S, Length))
} 
